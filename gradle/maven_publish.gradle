apply plugin: 'maven'

// aar 上传 到 maven 带上源码
task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.source
}
artifacts {
    archives androidSourcesJar
}

afterEvaluate {
    android.libraryVariants.each { variant ->
        if ("release".equalsIgnoreCase(variant.name) && tasks.findByName("uploadArchives") != null) {
            def androidSourcesJarTask = tasks.findByName("androidSourcesJar")
            def cleanTask = tasks.findByName("clean")
            // 给uploadArchives执行前加入clean task
            if (androidSourcesJarTask != null && cleanTask != null) {
                androidSourcesJarTask.dependsOn cleanTask
            }
        }
    }
}

// 发布 aar 到 maven 仓库
uploadArchives {
    if (module_devEnable) {
            throw new GradleException("=============== 发布 aar 时 module_devEnable 不能置为 true ================== " +
                    "\n=============== 请在 gradle.properties 文件更改后重新发布 =================")
    }
    repositories {
        mavenDeployer {
            // maven 仓库地址
            repository(url: NEXUS_MAVEN_URL) {
                authentication(userName: NEXUS_USER_NAME, password: NEXUS_PASSWORD)
            }
            pom.version = VERSION
            pom.artifactId = ARTIFACT_ID
            pom.groupId = GROUP_ID
        }
    }
}